plugins {
    id 'com.android.application'
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")
// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.jiva.mandi"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        all {
            resValue("string", "RSA_PUB_KEY", keystoreProperties['RSA_PUB_KEY'] ?: "")
        }
        release {
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {


    // App dependencies
    def app_compat_version = '1.5.1'
    def material_version = '1.6.1'
    def annotation_version = '1.4.0'
    def constraint_layout_version = '2.1.4'
    def lifecycle_version = '2.5.1'
    def room_version = "2.4.3"
    def junit_version = '4.13.2'
    def truth_version = '1.1.3'
    def ext = '1.1.3'
    def dagger_2_version = '2.35.1'
    def data_binding_version = '7.3.0'
    def rx_android_version = '2.1.1'
    def gson_version = '2.9.1'
    def nav_version = "2.5.2"
    def runner="1.0.2"
    def mockito="2.22.0"

    implementation "androidx.appcompat:appcompat:$app_compat_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.annotation:annotation:$annotation_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.databinding:databinding-runtime:$data_binding_version"

    // RxJava2 support for Room
    implementation "androidx.room:room-rxjava2:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rx_android_version"

    // Dagger2
    implementation "com.google.dagger:dagger-android:$dagger_2_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_2_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_2_version"

    // GSON
    implementation "com.google.code.gson:gson:$gson_version"

    // Java language implementation
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"

    // Testing dependency
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "com.android.support.test:runner:$runner"
    androidTestImplementation "androidx.test.ext:junit:$ext"
    testImplementation "com.google.truth:truth:$truth_version"
    androidTestImplementation "com.google.truth:truth:$truth_version"
    testImplementation "org.mockito:mockito-core:$mockito"
    androidTestImplementation "org.mockito:mockito-core:$mockito"
}